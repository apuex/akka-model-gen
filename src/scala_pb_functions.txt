. function pb_find_entity_by_type(type)
  for model.entity
    if entity.name = my.type
      return entity
    else
      for entity.aggregation
        if aggregation.name = my.type
          return aggregation
        endif
      endfor
    endif
  endfor
.endfunction

.function pb_format_type(t)
  if my.t = "tinyint" | my.t = "bool"
    return "bool"
  elsif my.t = "int"
    return "int32"
  elsif my.t = "bigint"
    return "int64"
  elsif my.t = "double" | my.t = "string"
    return "$(my.t:no)"
  elsif my.t = "timestamp"
    return "google.protobuf.Timestamp"
  else
    ref = pb_find_entity_by_type(my.t)
    if defined(ref.enum) & ref.enum = "true"
      return "$(my.t:Pascal)"
    else
      return "$(my.t:Pascal)Vo"
    endif
  endif
.endfunction

.function pb_output_field(field, fieldNo)
  if my.field.type = "array"
    if defined(my.field.entity)
      valueField = model->entity(name = my.field.entity)->field(name = my.field.valueField)
      return "repeated $(pb_format_type(valueField.valueType)) $(valueField.name:Camel) = $(my.fieldNo);"
    elsif my.field.valueType = "char"
      if defined(my.field.required) & my.field.required = "true"
        return "required bytes $(my.field.name:Camel) = $(my.fieldNo);"
      else
        return "optional bytes $(my.field.name:Camel) = $(my.fieldNo);"
      endif
    else
      return "repeated $(pb_format_type(my.field.valueType)) $(my.field.name:Camel) = $(my.fieldNo);"
    endif
  elsif my.field.type = "map"
    if defined(my.field.entity)
      kField = model->entity(name = my.field.entity)->field(name = my.field.keyField)
      vField = model->entity(name = my.field.entity)->field(name = my.field.valueField)
      return "$(my.field.type)<$(pb_format_type(kField.type)), $(pb_format_type(vField.type))> $(my.field.name:Camel) = $(my.fieldNo);"
    else
      return "$(my.field.type)<$(pb_format_type(my.field.keyType)), $(pb_format_type(my.field.valueType))> $(my.field.name:Camel) = $(my.fieldNo);"
    endif
  else
    if defined(my.field.required) & my.field.required = "true"
      return "required $(pb_format_type(my.field.type)) $(my.field.name:Camel) = $(my.fieldNo);"
    else
      return "optional $(pb_format_type(my.field.type)) $(my.field.name:Camel) = $(my.fieldNo);"
    endif
  endif
.endfunction

.function pb_extract_pk(entity)
  for my.entity.primaryKey
    return primaryKey
  endfor
.endfunction

.function pb_output_pk(entity, fieldNo)
  result = ""
  pk = pb_extract_pk(my.entity)
  if defined(pk)
    for pk.field
      pkField = field
      if defined(my.entity->field(name = pkField.name))
        theField = my.entity->field(name = pkField.name)
        if !first()
          result = result + "\n  "
        endif
        result = result + "$(pb_output_field(theField, fieldNo))"
        fieldNo = fieldNo + 1
      else
        theField = extract_fk_field(my.entity, field.name)
        result = result + "$(pb_output_field(theField, fieldNo))"
        fieldNo = fieldNo + 1
      endif
    endfor
  else
    abort "E: no primaryKey is defined on entity $(my.entity.name)."
  endif
  return result
.endfunction

.function pb_output_fk(entity, fieldNo)
  result = ""
  fk = entity->foreignKey(refEntity = my.entity.aggregationTo)
  theEntity = my.entity
  ref = model->entity(name = theEntity.aggregationTo)
  if defined(fk) & defined(ref)
    for fk.field
      fkField = field
      f = ref->field(name = fkField.name)
      field.type = f.type
      if defined(f.length)
        field.length = f.length
      endif
      if !first()
        result = result + "\n"
      endif
      result = result + "$(pb_output_field(field, fieldNo))"
      fieldNo = fieldNo + 1
    endfor
  else
    abort "E: no foreignKey or aggregationTo is defined on entity $(entity.name)."
  endif
  return result
.endfunction

