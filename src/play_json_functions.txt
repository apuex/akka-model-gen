.function json_output_format_with_dependency(entity)
  my.names = XML.new("ol")
  my.result = ""
# FIXME: shortcuts: add response as universal dependency
  my.names = add_dependency_by_entity_name("", "response", my.names)
# add dependency
  my.names = json_get_depended_entity_names(my.entity, my.names)
# add its own
  my.names = add_dependency_by_entity_name("", my.entity.name, my.names)
# output results
  for my.names.li
    my.type = "$(li)"
    if my.type = "timestamp"
      my.result = my.result + "  implicit val timestampFormat = TimestampFormat.format\n"
    elsif is_element_type(my.type)
    elsif my.type = "array"
      #my.result = my.result + "  byteStringFormat = ByteStringFormat.format\n"
    elsif my.type = "map"
    elsif my.type = "actor_ref"
    elsif my.type = "any"
    elsif is_enum_type(my.type)
      my.result = my.result + "  implicit val $(my.type:Camel)Format = EnumFormat.format($(my.type:Pascal))\n"
      my.result = my.result + "  implicit val $(my.type:Camel)VoFormat = Json.format[$(my.type:Pascal)Vo]\n"
    else
      my.result = my.result + "  implicit val $(my.type:Camel)VoFormat = Json.format[$(my.type:Pascal)Vo]\n"
    endif
  endfor
  return my.result
.endfunction

.function add_dependency_by_entity_name(entity_name, depended_name, names)
  if !defined(my.names.$(my.depended_name))
    if !(my.entity_name = my.depended_name)
      my.result = json_get_depended_entity_names(find_entity_by_type(my.depended_name), my.names)
    endif
    if !defined(my.names.$(my.depended_name))
      define my.names.$(my.depended_name) = "true"
      my.names.load_string("<li>$(my.depended_name)</li>")
    endif
  endif #!defined(my.names.$(my.depended_name))
  return my.names
.endfunction

.function json_get_depended_entity_names(entity, names)
  if !defined(my.entity)
    return my.names
  endif
  for my.entity.field
    if defined(field.type) \
      & !(defined(field.valueType) | defined(field.entity)) \
      & !is_element_type(field.type)
      my.names = add_dependency_by_entity_name(my.entity.name, field.type, my.names)
    elsif defined(field.entity)
      my.names = add_dependency_by_entity_name(my.entity.name, field.entity, my.names)
    elsif defined(field.valueType)
      my.names = add_dependency_by_entity_name(my.entity.name, field.valueType, my.names)
    endif #!is_element_type(my.type)
    for field.query
      if !defined(my.names.$(query.entity))
        my.names = add_dependency_by_entity_name(my.entity.name, query.entity, my.names)
      endif #!defined(my.names.$(query.entity))
    endfor
  endfor
  return my.names
.endfunction
